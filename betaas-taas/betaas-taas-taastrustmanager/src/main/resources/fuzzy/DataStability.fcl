FUNCTION_BLOCK dataStabilityAspect	

VAR_INPUT
	random : REAL;
	variance : REAL;
	coherence : REAL;	
END_VAR

VAR_OUTPUT
	trust : REAL;
END_VAR

FUZZIFY random			
	TERM yes := gbell 0.5 2 1; 
	TERM no := gbell 0.5 2 2;
END_FUZZIFY

FUZZIFY variance			
	TERM no_ok := gbell 0.5 2 1; 
	TERM ok := gbell 0.5 2 2; 
END_FUZZIFY

FUZZIFY coherence			
	TERM no_ok := gbell 0.5 2 1; 
	TERM unknown := gbell 0.5 2 2;
	TERM ok := gbell 0.5 2 3;  
END_FUZZIFY

DEFUZZIFY trust
	TERM veryLow := gauss 0.25 1.4; 
	TERM low := gauss 1.70 0.6;
	TERM medium := gauss 2.70 0.6;
	TERM high := gauss 3.70 0.6;
	TERM veryHigh := gauss 4.95 1.4;
	METHOD : COG;		// Use 'Center Of Gravity' defuzzification method
	DEFAULT := 2.5;		// Default value is 2.5 (if no rule activates defuzzifier)
END_DEFUZZIFY

RULEBLOCK No1
	AND : MIN;			// Use 'min' for 'and' (also implicit use 'max' for 'or')
	ACT : MIN;			// Use 'min' activation method
	ACCU : MAX;			// Use 'max' accumulation method

	RULE 1 : IF variance IS no_ok AND random IS yes THEN trust IS veryLow;
	RULE 2 : IF variance IS no_ok AND random IS no AND coherence IS unknown THEN trust IS low;
	RULE 3 : IF variance IS no_ok AND random IS yes AND coherence IS ok THEN trust IS low;	
	RULE 4 : IF variance IS no_ok AND random IS no AND coherence IS ok THEN trust IS medium;
	RULE 5 : IF variance IS ok AND random IS no AND coherence IS no_ok THEN trust IS medium;
	RULE 6 : IF variance IS ok AND random IS no AND coherence IS unknown THEN trust IS high;
	RULE 7 : IF variance IS ok AND random IS no AND coherence IS ok THEN trust IS veryHigh;
END_RULEBLOCK

END_FUNCTION_BLOCK

